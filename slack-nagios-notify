#!/usr/bin/perl
use strict;

use HTTP::Request::Common qw(POST);
use LWP::UserAgent;
use JSON;
use Getopt::Long;

my $token = 'XXXX';
my $default_channel = '#technical';

my ($icingaBaseUrl,$color,$target,$channel,$users,$type,$host,$service,$timestamp,$serviceState,$hostState,$output,$ackAuthor,$ackComment);

GetOptions( "channel=s"       => \$channel,       # slack channel name  $CONTACTEMAIL$
            "token=s"         => \$token,         # slack integration token
            "target=s"        => \$target,        # notification target - host or service
            "baseurl=s"       => \$icingaBaseUrl, # icinga base URL     $CONTACTADDRESS1$
            "users=s"         => \$users,         # slack mentions      $CONTACTPAGER$
            "type=s"          => \$type,          # notification type   $NOTIFICATIONTYPE$
            "host=s"          => \$host,          # host name           $HOSTNAME$
            "service=s"       => \$service,       # service desc        $SERVICEDESC$
            "timestamp=s"     => \$timestamp,     # timestamp           $SHORTDATETIME$
            "servicestate=s"  => \$serviceState,  # service state       $SERVICESTATE$
            "hoststate=s"     => \$hostState,     # host state          $HOSTSTATE$
            "output=s"        => \$output,        # service output      $SERVICEOUTPUT$
            "ackauthor=s"     => \$ackAuthor,     # ack author          "$HOSTACKAUTHOR$ $SERVICEACKAUTHOR$"
            "ackcomment=s"    => \$ackComment,    # ack comment         "$HOSTACKCOMMENT $SERVICEACKCOMMENT$"
        );

my $mention;
$mention = "$users icinga notification: *$type*" if($users ne '');

my $payload = {
                channel => $channel // $default_channel,
                username => 'Icinga',
                icon_emoji => ':construction:',
                text =>  $mention,
                link_names => 1,
              };

if ($type eq '' or $target eq '') {
    die "must specify type and target\n";
} elsif ($type eq 'PROBLEM') {
    if ($target eq 'HOST') {
        $color = '#000000';
    } elsif ($target eq 'SERVICE') {
        $color = 'danger';
    }
} elsif ($type eq 'RECOVERY') {
    $color = 'good';
} elsif ($type eq 'ACKNOWLEDGEMENT') {
    $color = '#439FE0';
    $output = "author: $ackAuthor comment: $ackComment";
}

if ($target eq 'HOST') {
    $payload->{attachments} = [{
        fallback => "$timestamp: $host $hostState $output",
        text => "Host *$host* is *$hostState*",
        color => $color,
        mrkdwn_in => ["text"],
        fields => [
            {
                title => 'Timestamp',
                value => $timestamp,
                short => 'false',
            }
        ],
    }];
} elsif ($target eq 'SERVICE') {
    $payload->{attachments} = [{
        fallback => "$timestamp: $host/$service $serviceState $output",
        text => "Service *$service* on $host is *$serviceState*",
        color => $color,
        mrkdwn_in => ["text"],
        fields => [
            {
                title => 'Detailed information',
                value => $output,
                short => 'false',
            },
            {
                title => 'Timestamp',
                value => $timestamp,
                short => 'false',
            }
        ],
    }];
}

my $ua = LWP::UserAgent->new;
$ua->timeout(15);

my $req = POST("https://hooks.slack.com/services/${token}", ['payload' => encode_json($payload)]);

my $resp = $ua->request($req);

if ($resp->is_success) {
  #print $resp->decoded_content;  # or whatever
}
else {
  print $resp->decoded_content;  # or whatever
  die $resp->status_line;
}
exit(0);
